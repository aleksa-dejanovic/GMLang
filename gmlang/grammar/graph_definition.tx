import common

GraphDefinitionCommand:
    ConnectionCommand | NodeCreationCommand | GraphContextCommand
;

ConnectionOperator:
    '->' | '--' | '<-'
;

LConnectionOperator:
    '<' | '-'
;

RConnectionOperator:
    '>' | '-'
;

ConnectionCommand:
    StandardConnectionCommand | InfixConnectionCommand
;

StandardConnectionCommand:
    first=NodeSet operator=ConnectionOperator second=NodeSet
    (attr_list=AttributeList | tag=ID )?
;

NodeSet:
    nodes=Identifier
    | ('{' nodes+=Identifier[','] '}')
;

InfixConnectionCommand:
    first=NodeSet LConnectionOperator tag=ID RConnectionOperator second=NodeSet
;

NodeCreationCommand:
    'node' node=Identifier attr_list=AttributeList?
;

AttributeList:
    '[' attributes*=Attribute[','] ']'
;

Attribute:
    (key=ID | key=STRING) ':' (value=STRING | value=NUMBER)
;

GraphContextCommand:
    'graph' subcommand=ID args*=Parameter kwargs*=KWParameter
;


Parameter:
    STRING | NUMBER | ID
;
KWParameter:
    '-'('-'?) key=ID (value=STRING | value=NUMBER | value=ID)?
;

CreateGraphCommand:
    'create' graph_name=ID
;

UseGraphCommand:
    'use' graph_name=ID
;

ListGraphsCommand:
    'list'
;

DropGraphCommand:
    'drop' graph_name=ID
;