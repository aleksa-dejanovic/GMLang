import common

GraphDefinitionCommand:
    ConnectionCommand
    | NodeCreationCommand
;

ConnectionOperator:
    '->' | '--' | '<-' | '<>'
;

LConnectionOperator:
    '<' | '-'
;

RConnectionOperator:
    '>' | '-'
;

HyperEdgeOperator:
    '*--' | '*->' | '*<-' | '<*>'
;

Nodes:
    NodeCreationCommand
    | ('(' NodeCreationCommand ')')
    | NodeSet
;

ConnectionCommand:
    StandardConnectionCommand
    | InfixConnectionCommand
    | HyperEdgeChain
;

StandardConnectionCommand:
    (scc=SCCNoAttributes (attr_list=AttributeList | tag=ID )?)
    | ('(' scc=SCCNoAttributes ')' (attr_list=AttributeList | tag=ID )?)
;

SCCNoAttributes:
    first=Nodes operator=ConnectionOperator second=Nodes
;

NodeSet:
    nodes=Identifier
    | ('{' nodes+=Identifier[','] '}')
;

NodeList:
    nodes+=Identifier[',']
;

InfixConnectionCommand:
    first=Nodes l_opr=LConnectionOperator tag=ID r_opr=RConnectionOperator second=Nodes
;

NodeCreationCommand:
    'node' nodes=NodeList attr_list=AttributeList?
;

AttributeList:
    '[' attributes*=Attribute[','] ']'
;

Attribute:
    (key=ID | key=STRING) ':' (value=STRING | value=NUMBER)
;

HyperEdgeChain:
    edges+=HyperEdge attr_list=AttributeList?
;

HyperEdge:
    operator=HyperEdgeOperator inner=Nodes
;

